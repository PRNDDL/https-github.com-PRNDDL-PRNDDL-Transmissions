type User {
  displayName: String,
  email: String,
  photoURL: String,
  uid: String,
  repairs: Boolean[],
  isManager: Boolean | Null,
}

type Comment {
  validate() { root.users[this.user].exists() }

  message: String,
  datetime: Number,
  user: String,
}

type Repair {
  validate() { !this.user || root.users[this.user].exists() }

  title: String,
  description: String,
  datetime: Number,
  completed: Boolean,
  approved: Boolean,
  user: String,
  comments: Comments[],
}

path /users {
  read() { auth != null }
}

path /users/{uid} is User {
  validate() { this.uid == uid }

  create() { isManager() ||  isCurrentUser(uid) }
  read()   { auth != null }
  update() { isManager() ||  isCurrentUser(uid) }
  delete() { isManager() && !isCurrentUser(uid) }
}

path /users/{uid}/isManager {
  validate() { this == prior(this) || (isManager() && !isCurrentUser(uid)) }
  write() { isManager() && !isCurrentUser(uid) }
}

path /users/{uid}/repairs {
  write() { false }
}

path /repairs {
  read()  { isManager() }
  write() { isManager() }
}

path /repairs/{id} {
  read() { isManager() || isCurrentUser(this.user) }
}

path /repairs/{id}/comments {
  create() { isManager() || isCurrentUser(this.parent().user) }
  update() { isManager() || isCurrentUser(this.parent().user) }
}

path /repairs/{id}/completed {
  read()  { isManager() || isCurrentUser(this.parent().user) }
  write() { isManager() || (isCurrentUser(this.parent().user) && this == true) }
}

path /repairs/{id}/approved {
  validate() { this == false || this.parent().completed == true }
}

isCurrentUser(uid) { auth !== null && auth.uid === uid }

isManager() { auth !== null && prior(root.users[auth.uid].isManager) }
